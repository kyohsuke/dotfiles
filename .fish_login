# vim: filetype=fish foldmethod=marker

# {{{ Add Function Path
set -gx fish_function_path $fish_function_path "$HOME/.fish-functions"
# }}}
# {{{ Change Key Bindings
test $fish_key_bindings != fish_vi_key_bindings
and fish_vi_key_bindings
# }}}
# {{{ KeyBindings
function fish_user_key_bindings
    bind -s -M default -m insert \cc cancel-commandline repaint-mode force-repaint
    bind -s -M visual -m insert \cc end-selection cancel-commandline repaint-mode force-repaint

    bind -s -M insert \co __ghq_cd
    bind -s -M insert \ch __peco_history
    bind -s -M insert \ck __peco_branch

    # Accept Autosuggestion like python prompt Kit
    bind -s -M insert \ce accept-autosuggestion

    bind -s -M visual -m default d "fish_clipboard_copy; commandline -f kill-selection end-selection repaint-mode;"
    bind -s -M visual -m default y "fish_clipboard_copy; commandline -f end-selection repaint-mode;"

    bind -s -M default p fish_clipboard_paste
    bind -s -M default P fish_clipboard_paste
end
# }}}
# {{{ check_ssh_config
test -f "$HOME/.fish-functions/check_ssh_config.fish"
and source "$HOME/.fish-functions/check_ssh_config.fish"
# }}}
# {{{ Direnv
command -v direnv >/dev/null
and _evalcache direnv hook fish
# }}}
# {{{ Add home path
set -l ind (contains -i -- "$HOME/sbin" $PATH)
and set -e PATH[$ind]
set -gx PATH "$HOME/sbin" $PATH

set -l ind (contains -i -- "$HOME/bin" $PATH)
and set -e PATH[$ind]
set -gx PATH "$HOME/bin" $PATH
# }}}
# {{{ Aliases
set -f vim_version (vim --version | head -1 | sed 's/^.*\ \([0-9]\)\.\([0-9]\)\ .*$/\1\2/')

test -f "/Applications/MacVim.app/Contents/MacOS/Vim"
and alias vim='/Applications/MacVim.app/Contents/MacOS/Vim'

test -f "/usr/share/vim/vim$vim_version/macros/less.sh"
and alias less="/usr/share/vim/vim$vim_version/macros/less.sh"

abbr -a gup git pull --rebase
abbr -a rm rm -i
abbr -a df df -h
abbr -a bc bc -l
abbr -a scrr screen -U -D -RR
abbr -a diff diff -W $COLUMNS
abbr -a locate mdfind -name
abbr -a top top -o cpu
abbr -a cat bat
abbr -a cp cp -v -i
abbr -a mv mv -v -i
abbr -a gce "gcloud compute ssh --zone=us-west1-a --tunnel-through-iap"
abbr -a youtube --set-cursor "streamlink '%'"

# Enable AWS CLI autocompletion: github.com/aws/aws-cli/issues/1079
command -v aws >/dev/null
and complete --command aws --no-files --arguments '(begin; set --local --export COMP_SHELL fish; set --local --export COMP_LINE (commandline); aws_completer | sed \'s/ $//\'; end)'


command -v docker-compose >/dev/null
and alias dcs='docker stats $(docker-compose ps -q || echo "#")'

command -v thefuck >/dev/null
and abbr -a f thefuck
# }}}
# {{{ Functions
if command -v zpaq >/dev/null
    function zpack -a dirName
        echo (basename $dirName) | xargs -I{} zpaq a "{}.zpaq" "{}" -m5 -not "*/.DS_Store"
    end
end
# }}}

# {{{ Transient
if type -q __transient
    functions --copy fish_prompt transient_prompt_func
    function transient_rprompt_func
        date +%T
    end
    function fish_right_prompt
        date +%T
    end
end
# }}}

test -f "$HOME/.fish_login.local"
and source "$HOME/.fish_login.local"
