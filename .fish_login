# vim: filetype=fish foldmethod=marker

# {{{ Add Function Path
set -gx fish_function_path $fish_function_path "$HOME/.fish-functions"
# }}}
# {{{ Change Key Bindings
test $fish_key_bindings != fish_vi_key_bindings
and fish_vi_key_bindings
# }}}
# {{{ KeyBindings
function fish_user_key_bindings
    bind -s -M default -m insert \cc cancel-commandline repaint-mode force-repaint
    bind -s -M visual -m insert \cc end-selection cancel-commandline repaint-mode force-repaint

    bind -s -M insert \co __ghq_cd
    bind -s -M insert \ch __peco_history
    bind -s -M insert \ck __peco_branch

    # Accept Autosuggestion like python prompt Kit
    bind -s -M insert \ce accept-autosuggestion

    bind -s -M visual -m default d "fish_clipboard_copy; commandline -f kill-selection end-selection repaint-mode;"
    bind -s -M visual -m default y "fish_clipboard_copy; commandline -f end-selection repaint-mode;"

    bind -s -M default p fish_clipboard_paste
    bind -s -M default P fish_clipboard_paste
end
# }}}
# {{{ check_ssh_config
test -f "$HOME/.fish-functions/check_ssh_config.fish"
and source "$HOME/.fish-functions/check_ssh_config.fish"
# }}}
# {{{ Direnv
command -v direnv >/dev/null
and _evalcache direnv hook fish
# }}}
# {{{ Add home path
set -l ind (contains -i -- "$HOME/sbin" $PATH)
and set -e PATH[$ind]
set -gx PATH "$HOME/sbin" $PATH

set -l ind (contains -i -- "$HOME/bin" $PATH)
and set -e PATH[$ind]
set -gx PATH "$HOME/bin" $PATH
# }}}
# {{{ Aliases
set -f vim_version (vim --version | head -1 | sed 's/^.*\ \([0-9]\)\.\([0-9]\)\ .*$/\1\2/')
alias vim='/Applications/MacVim.app/Contents/MacOS/Vim'
alias less="/usr/share/vim/vim$vim_version/macros/less.sh"

abbr -a gup git pull --rebase
abbr -a rm rm -i
abbr -a df df -h
abbr -a bc bc -l
abbr -a scrr screen -U -D -RR
abbr -a diff diff -W $COLUMNS
abbr -a locate mdfind -name
abbr -a top top -o cpu
abbr -a cat bat --paging=never
abbr -a ls ls -G
abbr -a cp cp -v -i
abbr -a mv mv -v -i

command -v docker-compose >/dev/null
and alias dcs='docker stats $(docker-compose ps -q || echo "#")'

command -v thefuck >/dev/null
and abbr -a f thefuck
# }}}
