# vim: filetype=fish foldmethod=marker

# {{{ rustup
test -d "$HOME/.cargo/bin"
and set -l ind (contains -i -- "$HOME/.cargo/bin" $PATH)
and set -e PATH[$ind]
set -gx PATH "$HOME/.cargo/bin" $PATH
# }}}
# {{{ docker-machine
command -v docker-machine >/dev/null
and _evalcache docker-machine env default
# }}}

# {{{ Homebrew
test -x "/usr/local/bin/brew"
and set -l BREW_BIN "/usr/local/bin/brew"

test -x "/opt/homebrew/bin/brew"
and set -l BREW_BIN "/opt/homebrew/bin/brew"

test -x "/home/linuxbrew/.linuxbrew/bin/brew"
and set -l BREW_BIN "/home/linuxbrew/.linuxbrew/bin/brew"

test -x "$BREW_BIN"
and _evalcache "$BREW_BIN" shellenv
# }}}

# {{{ anyenv
if test -x "$HOME/.anyenv/bin/anyenv"
    set -l ind (contains -i -- "$HOME/.anyenv/bin" $PATH)
    and set -e PATH[$ind]
    set -gx PATH "$HOME/.anyenv/bin" $PATH

    _evalcache anyenv init - fish
    set -f envPath "$HOME/.anyenv/envs"

    # goenv
    if test -x "$envPath/goenv/bin/goenv"
        set -gx GOENV_DISABLE_GOPATH 1
        set -gx GOPATH "$HOME"
        set -gx GOBIN "$HOME/bin"
        set -gx GOCACHE "$HOME/.cache/go-build"
        set -gx GOMODCACHE "$HOME/.cache/pkg/mod"
    end

    # pyenv
    if test -x "$envPath/pyenv/bin/pyenv"
        set -gx PYTHONDONTWRITEBYTECODE 1
        set -gx PYENV_VIRTUALENV_DISABLE_PROMPT 1
        switch $DETECT_OSTYPE
            case Darwin
                set -gx PYTHON_CONFIGURE_OPTS "--enable-framework --enable-optimizations --with-lto"
            case Linux
                set -gx PYTHON_CONFIGURE_OPTS "--enable-optimizations --with-lto"
        end
        set -gx PYTHONWARNINGS 'ignore::DeprecationWarning'
    end

    # rbenv
    if test -x "$envPath/rbenv/bin/rbenv"
        set -gx GEM_EDITOR mvim
        set -gx RUBY_YJIT_ENABLE 1
        set -gx UNBUNDLED_COMMANDS rake cap irb
        set -gx RUBY_CONFIGURE_OPTS "--disable-install-doc --disable-install-rdoc"
    end
end
# }}}

# {{{ google cloud SDK
test -f "$HOMEBREW_PREFIX/share/google-cloud-sdk/path.fish.inc"
and source "$HOMEBREW_PREFIX/share/google-cloud-sdk/path.fish.inc"
# }}}

if test -d "$HOMEBREW_PREFIX/opt/coreutils"
    set -gx PATH "$HOMEBREW_PREFIX/opt/coreutils/libexec/gnubin" $PATH
    set -gx MANPATH "$HOMEBREW_PREFIX/opt/coreutils/libexec/gnuman" $MANPATH
end

# {{{ util-linux
# }}}
if test -d "$HOMEBREW_PREFIX/opt/util-linux"
    set -gx PATH "$HOMEBREW_PREFIX/opt/util-linux/bin" $PATH
    set -gx PATH "$HOMEBREW_PREFIX/opt/util-linux/sbin" $PATH
    set -gx MANPATH "$HOMEBREW_PREFIX/opt/util-linux/share/man" $MANPATH
end

set -l upath
for npath in $PATH
    not contains -- $npath $upath
    and set upath $upath $npath
end
set -gx PATH $upath

test -f "$HOME/.fishrc.local"
and source "$HOME/.fishrc.local"
